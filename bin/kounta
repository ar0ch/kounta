#!/usr/bin/env perl
use 5.26.0;
use strict;
use FindBin;
use lib "$FindBin::RealBin/../perl5";
use Biotool::Getopt;
use Biotool::Logger;
use Biotool::Binaries;
use Data::Dumper;
use File::Temp;
use File::Basename;
use File::Path qw(make_path remove_tree);

sub run_cmd {
  my($cmd) = @_;
  msg("Running: $cmd");
  system($cmd)==0 or err("Could not run: $cmd");
}

sub main {
  my $TAB = q{$'\t'}; # literal tab in /bin/sh

  my $opt = Biotool::Getopt->getopt( 
  {
    name => 'kounta',
    version => '0.1.4',
    desc => 'Build a multi-genome k-mer count matrix',
    url => 'https://github.com/tseemann/kounta',
  },
  {
    out => { type=>'file', need=>1, desc=>"Output matrix file" },
    kmer => { type=>'int', default=>25, need=>1, desc=>"k-mer length" },
    threads => { type=>'int', default=>1, need=>1, desc=>"Threads to use" },
    ram => { type=>'int', default=>4, need=>1, desc=>"RAM in gigabytes to use" },
    minfreq => { type=>'int', default=>3, need=>1, desc=>"Min k-mer frequency (FASTQ only)" },
    tempdir => { type=>'string', default=>'auto', desc=>"Fast working directory" }
#    binary => { type=>'', desc=>"Output binary presence/absence instead of counts" },
  });

#  print Dumper($opt);
  require_exe($_) for qw(sort join paste uniq parallel kmc kmc_tools);
#  return if $opt->{check};

  $$opt{ARGV} or err("Please provide some contig or read files");
  my @seqfile = $opt->{ARGV}->@*;
  map { -r $_ or err("Can't read input file '$_'") } @seqfile;

  # use system tempdir() unless user requests a folder
  my $dir = $$opt{tempdir};
  if (!$dir or $dir eq 'auto') {
    $dir = File::Temp->newdir();
  }
  else {
    make_path($dir);
  }
  msg("Temp folder:", $dir);
  
  my $sort = sprintf "LC_ALL=C sort --parallel %d --buffer-size %dG -t $TAB",
                     $$opt{threads}, $$opt{ram};
  
  my @prefix;
  for my $path (@seqfile) {
    my $file = basename($path);
    push @prefix, $file;
    
    msg("Counting k-mers in $path");
    my $fmt = $file =~ m/\.f(ast)?q/ ? "-fq -ci".$$opt{minfreq} : "-fm -ci1";
    my $kmc = sprintf "kmc -cs65535 -m%d -sm -k%d -t%d $fmt $path $dir/$file $dir 1>/dev/null",
                $$opt{ram}, $$opt{kmer}, $$opt{threads};
    run_cmd($kmc);
    
    msg("Sorting k-mers in $file");    
    # https://github.com/refresh-bio/KMC/issues/131
    # my $dump = "kmc_dump $dir/$file /dev/stdout | $sort -k1,1d > $dir/$file.kmers";
    my $dump = "kmc_tools transform $dir/$file dump -s $dir/$file.kmers";
    run_cmd($dump);
    unlink "$dir/$file.kmc_suf";
    unlink "$dir/$file.kmc_pre";
    system("head -n 5 $dir/$file.kmers");
  }

  my $nsamp = scalar(@prefix);
  msg("Find unique kmers from $nsamp files");
  # we perform an efficient merge sort rather than cat+sort
  my $uniq = "$sort -m --batch-size=$nsamp $dir/*.kmers | cut -f1 | uniq > $dir/uniqmers";
  run_cmd($uniq);
  system("head -n 10 $dir/uniqmers");

  msg("Joining uniqmers to sample k-mer counts");
  my @pin = map { "$dir/$_.kmers" } @prefix;
  my $pjoin = "parallel -j $$opt{threads} -v"
            . " \"join -o '1.1 2.2' -j 1 -a 1 -t $TAB -e 0 $dir/uniqmers {}" 
            . " | cut -f2 > {.}.count\" ::: @pin"; 
  run_cmd($pjoin);

  # print header first
  msg("Writing header for output file:", $$opt{outmatrix});
  open my $OUT, '>', $$opt{outmatrix};
  print $OUT join("\t", '#KMER', @prefix)."\n";
  close $OUT;
  
  # append matrix
  msg("Combining $nsamp count files.");
  my @bits = map { "$dir/$_" } ('uniqmers', map { "$_.count" } @prefix);
  my $paste = "paste @bits >> ".$$opt{outmatrix};
  run_cmd($paste);

  # finish up
  system("wc -l $dir/*");  
  msg("Result in:", $$opt{outmatrix});
  msg("Done.");
  return 0;
}

exit main(@ARGV);

